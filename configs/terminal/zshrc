################################################################################
# GENERAL

# vi mode
bindkey -v
export KEYTIMEOUT=1

stty -ixon

# history
HISTFILE="$HOME/.zsh_history"
HISTSIZE=50000
SAVEHIST=10000

setopt hist_expire_dups_first
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_verify
setopt inc_append_history
setopt share_history

# completion
autoload -U compinit && compinit
zmodload -i zsh/complist

eval "$(dircolors)"
zstyle ':completion:*' menu select
zstyle ':completion:*' completer _complete _correct _approximate
zstyle ':completion:*:default' select-scroll 0

zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' format "%B--- %F{magenta}%d%f ---%b"
zstyle ':completion:*:options' list-colors "=(#b)*(-- [a-zA-Z0-9]*)=32=36"
zstyle ':completion:*:commands' list-colors "=(#b)*(-- [a-zA-Z0-9]*)=32=36"

# options
setopt correct
setopt auto_cd
setopt extended_glob

################################################################################
# VARIABLES

export PATH="$(yarn global bin):$PATH"
export PATH="$(npm -g bin):$PATH"
export PATH=~/.local/bin/:$PATH

export EDITOR="editor.sh"
PAGER="editor.sh -R"
export MANPAGER="nvim -c 'set ft=man' -"
export LESSKEY="$(realpath ~)/.lesskey"

# pass store
PASSWORD_STORE_CLIP_TIME=15
PASSWORD_STORE_GENERATED_LENGTH=17

################################################################################
# PLUGIN CONFIGS

FZF_TMUX_HEIGHT=40%
FZF_DEFAULT_OPTS='--no-mouse --cycle --reverse --border --color=bg:233'
FZF_DEFAULT_COMMAND='fd --type file'
FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
FZF_ALT_C_COMMAND='fd --type directory'

ZSH_AUTOSUGGEST_USE_ASYNC=1
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=#778077'

ZLE_RPROMPT_INDENT=0

zstyle ':filter-select' case-insensitive yes

################################################################################
# PLUGINS

source /usr/share/zsh/share/antigen.zsh

antigen bundle zdharma/fast-syntax-highlighting
antigen bundle zsh-users/zsh-autosuggestions
antigen bundle zsh-users/zsh-completions
antigen bundle backdround/zsh-vimode-visual

antigen bundle command-not-found
antigen bundle extract
antigen bundle transfer

antigen bundle djui/alias-tips
antigen bundle hlissner/zsh-autopair

antigen theme romkatv/powerlevel10k

antigen apply

# fzf
source /usr/share/fzf/completion.zsh
source /usr/share/fzf/key-bindings.zsh

# powerlevel10k
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

################################################################################
# ZLE

# missed default widgets
function vi-kill-word() {
  local WORDCHARS=${WORDCHARS/\//}
  zle kill-word
}
zle -N vi-kill-word

function custom-which-command() {
  zle autosuggest-clear
  zle which-command
}
zle -N custom-which-command

# x11 copy wrapping
function x11-copy-wrap-widgets() {
  for widget in $@; do
    eval "
    function _x11-copy-wrap-$widget() {
      zle .$widget
      xclip -in -selection clipboard <<<\$CUTBUFFER
    }"
    zle -N $widget _x11-copy-wrap-$widget
  done
}
local copy_widgets=(
  vi-yank vi-yank-eol
)
x11-copy-wrap-widgets $copy_widgets


# x11 paste wrapping
function x11-paste-wrap-widgets() {
  for widget in $@; do
    eval "
    function _x11-paste-wrap-$widget() {
        CUTBUFFER=\$(xclip -out -selection clipboard)
        zle .$widget
    }"
    zle -N $widget _x11-paste-wrap-$widget
  done
}
local paste_widgets=(
  vi-put-{before,after}
)
x11-paste-wrap-widgets $paste_widgets


# x11 copy whole line
vi-yank-x-line () {
  print -rn -- $BUFFER | xsel -i -p;
  print -rn -- $BUFFER | xsel -i -b;
}
zle -N vi-yank-x-line


# cd
function cd-project-root() {
  local root_directory="$(git rev-parse --show-toplevel 2> /dev/null)"
  if [ $? ]; then
    cd "$root_directory" > /dev/null && zle reset-prompt
  fi
}
function cd-home() { cd ~ > /dev/null && zle reset-prompt }
function cd-up() { cd .. > /dev/null && zle reset-prompt }
function cd-back() { cd - > /dev/null && zle reset-prompt }
zle -N cd-project-root
zle -N cd-home
zle -N cd-up
zle -N cd-back


# fzf wrappers
function fzf-cd-project() {
  zle -I
  UPDATE_GIT_DB='updatedb -l 0 -U ~ -e ~/tmp -o ~/.tmp/mlocate.db'
  FIND_GIT_DIRS="$UPDATE_GIT_DB && locate -d ~/.tmp/mlocate.db -b '\.git'"
  FZF_ALT_C_COMMAND="$FIND_GIT_DIRS | sed 's$\(.*\).git$\1$'" fzf-cd-widget
  zle reset-prompt
}
zle -N fzf-cd-project

function fzf-cd-widget-wrapper() {
  FZF_ALT_C_COMMAND='fd -I --type directory' FZF_DEFAULT_OPTS="--height 40% --reverse $FZF_DEFAULT_OPTS" fzf-cd-widget
}
zle -N fzf-cd-widget-wrapper


fzf-cd-file() {
  zle -I
  setopt localoptions pipefail no_aliases 2> /dev/null

  # Choose root directory
  local root_directory="$(git rev-parse --show-toplevel 2> /dev/null)"
  if [ -z "$root_directory" ]; then
    local root_directory=~
  fi

  # Find file
  local file="$(fd -tf . "$root_directory" | FZF_DEFAULT_OPTS="--height 40% --reverse $FZF_DEFAULT_OPTS" $(__fzfcmd))"
  if [[ -z "$file" ]]; then
    zle redisplay
    return 0
  fi

  # cd to file dir
  cd "$(dirname $file)"

  local ret=$?
  zle fzf-redraw-prompt
  return $ret
}
zle -N fzf-cd-file


vi_bind() {
  bindkey -M vicmd $@
  bindkey -M viins $@
}

################################################################################
# KEYBINDINGS

########################################
# KEY FIXES
#        Backspace
vi_bind '^?'    backward-delete-char
#        Delete
vi_bind "^[[3~" delete-char
#        Home
vi_bind "^[[H"  beginning-of-line
#        End
vi_bind "^[[F"  end-of-line
#        Page down
vi_bind "[6~" end-of-buffer-or-history
#        Page up
vi_bind "[5~" beginning-of-buffer-or-history
#        Insert
vi_bind "[2~" vi-insert
#        Shift-tab
vi_bind "[Z"  reverse-menu-complete

########################################
# VISUAL KEYBINDINGS

bindkey -r -M vivis 'jj'
bindkey -r -M vivis 'o'

# movements
bindkey -M vivis 'w' vi-visual-backward-word
bindkey -M vivis 'q' vi-visual-backward-word-end
bindkey -M vivis 'j' vi-visual-forward-word
bindkey -M vivis 'p' vi-visual-forward-word-end

bindkey -M vivis 'W' vi-visual-backward-blank-word
bindkey -M vivis 'Q' vi-visual-backward-blank-word-end
bindkey -M vivis 'J' vi-visual-forward-blank-word
bindkey -M vivis 'P' vi-visual-forward-blank-word-end

bindkey -M vivis 'z' vi-visual-find-prev-char
bindkey -M vivis 'Z' vi-visual-find-prev-char-skip
bindkey -M vivis 'k' vi-visual-find-next-char
bindkey -M vivis 'K' vi-visual-find-next-char-skip
bindkey -M vivis ')' vi-visual-repeat-find
bindkey -M vivis '(' vi-visual-rev-repeat-find

bindkey -M vivis 'on' vi-visual-eol
bindkey -M vivis 'od' vi-visual-bol
bindkey -M vivis 'oh' vi-visual-goto-line
bindkey -M vivis 'ot' vi-visual-goto-first-line

# editing
bindkey -M vivis 'g' vi-visual-insert-bol
bindkey -M vivis 'G' vi-visual-insert-bol
bindkey -M vivis 'h' vi-visual-change
bindkey -M vivis 'H' vi-visual-change
bindkey -M vivis "'" vi-visual-kill-and-vicmd
bindkey -M vivis 't' vi-visual-kill-and-vicmd
bindkey -M vivis 'T' vi-visual-kill-and-vicmd
bindkey -M vivis ',' vi-visual-replace-region

bindkey -M vivis 'm' vi-visual-lowercase-region
bindkey -M vivis 'M' vi-visual-uppercase-region

bindkey -M vivis '^M' vi-visual-yank
bindkey -M vivis 'f'  vi-visual-yank
bindkey -M vivis 'l'  vi-visual-put

# misc
bindkey -M vivis "n'" vi-visual-surround-squote
bindkey -M vivis 'n ' vi-visual-surround-space
bindkey -M vivis 'n"' vi-visual-surround-dquote
bindkey -M vivis 'n(' vi-visual-surround-parenthesis
bindkey -M vivis 'n)' vi-visual-surround-parenthesis

bindkey -M vivis '^[' vi-visual-exit
bindkey -M vivis 'N'  vi-visual-exit-to-vlines
bindkey -M vivis 'r'  vi-visual-exchange-points
bindkey -M vivis '"'  vi-visual-join


# visual line
bindkey -r -M vivli 'o'

bindkey -M vivli 'oh' vi-vlines-goto-line
bindkey -M vivli 'ot' vi-vlines-goto-first-line

bindkey -M vivli 'r' vi-vlines-exchange-points

bindkey -M vivli 'l' vi-vlines-put
bindkey -M vivli 'L' vi-vlines-put

bindkey -M vivli 'j' vi-vlines-down-line
bindkey -M vivli 'k' vi-vlines-up-line
bindkey -M vivli 'n' vi-vlines-exit-to-visual

########################################
# INSERT KEYBINDINGS

# movements
bindkey -v '^w' vi-backward-word
bindkey -v '^q' vi-backward-word-end
bindkey -v '^j' vi-forward-word
bindkey -v '^p' vi-forward-word-end

bindkey -v '^a' vi-backward-blank-word
bindkey -v '^o' vi-backward-blank-word-end
bindkey -v '^e' vi-forward-blank-word
bindkey -v '^u' vi-forward-blank-word-end

bindkey -v '^k' vi-find-next-char
bindkey -v '^z' vi-find-prev-char

########################################
# NORMAL KEYBINDINGS

bindkey -a -r 'o'
bindkey -a -r 'gg'

# movement
bindkey -a 'w' vi-backward-word
bindkey -a 'q' vi-backward-word-end
bindkey -a 'j' vi-forward-word
bindkey -a 'p' vi-forward-word-end

bindkey -a 'W' vi-backward-blank-word
bindkey -a 'Q' vi-backward-blank-word-end
bindkey -a 'J' vi-forward-blank-word
bindkey -a 'P' vi-forward-blank-word-end

bindkey -a 'od' beginning-of-line
bindkey -a 'on' end-of-line
bindkey -a 'oh' end-of-buffer-or-history
bindkey -a 'ot' beginning-of-buffer-or-history

bindkey -a 'z' vi-find-prev-char
bindkey -a 'Z' vi-find-prev-char-skip
bindkey -a 'k' vi-find-next-char
bindkey -a 'K' vi-find-next-char-skip
bindkey -a ')' vi-repeat-find
bindkey -a '(' vi-rev-repeat-find


# editing
bindkey -a 'g' vi-insert
bindkey -a 'G' vi-insert-bol
bindkey -a 'c' vi-add-next
bindkey -a 'C' vi-add-eol

bindkey -a 'h' vi-change
bindkey -a 'H' vi-change-eol
bindkey -a 't' vi-delete
bindkey -a 'T' vi-kill-eol
bindkey -a 'n' vi-visual-mode
bindkey -a 'N' vi-vlines-mode

bindkey -a 'm' undo
bindkey -a 'M' redo

bindkey -a 'f' vi-yank
bindkey -a 'F' vi-yank-eol
bindkey -a 'l' vi-put-after
bindkey -a 'L' vi-put-before

bindkey -a '"' vi-join
bindkey -a "'" vi-delete-char
bindkey -a ',' vi-replace-chars

########################################
# CUSTOM KEYBINDINGS

# misc
stty intr "^C"
stty eof "^G"
vi_bind '^r' clear-screen
vi_bind '^d' custom-which-command
vi_bind '^b' vi-quoted-insert

# editing
vi_bind '^t' kill-buffer

vi_bind '\e^h' backward-kill-word
vi_bind '^h' vi-backward-kill-word
vi_bind '\e^n' kill-word
vi_bind '^n' vi-kill-word

# copy / paste
vi_bind '^f' vi-yank-x-line
vi_bind '^l' vi-put-before

# cd
vi_bind '\em' cd-home
vi_bind '\e,' cd-up
vi_bind '\e.' cd-back


# applications
vi_bind '\eg' fzf-file-widget
vi_bind '\ec' fzf-cd-widget-wrapper
vi_bind '\er' fzf-history-widget

vi_bind '\eh' fzf-cd-project
vi_bind '\et' cd-project-root
vi_bind '\en' fzf-cd-file

vi_bind '\eu' autosuggest-execute
vi_bind '\ee' autosuggest-accept
vi_bind '\eo' autosuggest-clear


################################################################################
# ALIASES

# hacks
alias sudo='sudo '
alias n='nvim'
e() { eval "$EDITOR $*" }
alias p='python'
alias f='fd -IH'
alias -g .ps='$(fd ".*gpg" ~/.password-store/ | sed "s$.*\.password-store/\(.*\)\.gpg$\1$" | fzf --preview="pass show {}")'
alias psc='pass show --clip .ps'
alias tldr='tldr -c'
alias hdi='howdoi'

pg() { eval "pgrep -i -l \"$*\" | grep -i \"$*\"" }

sp() { eval "sudo pacman -$*" }
S() { eval "sudo pacman -S $*" }
Ss() { eval "sudo pacman -Ss $*" }

# scripts
t() { trans en:ru "$*" }
tr() { trans ru:en "$*" }
alias se='~/.local/bin/spellchecker'
alias sr='~/.local/bin/spellchecker_ru'
alias lolclock='while true; do tput clear; date +"%H : %M : %S" | figlet ; sleep 1; done'

# usage: fswatch-write ./file.txt echo "file was changed"
fswatch-write() {
eval "fswatch --event MovedTo --event MovedFrom --event Renamed --event Removed \
  --event Updated --event Created -o -r $1 | xargs -i sh -c \"${@:2}\""
}

# git
alias g='git'
alias gs='git status'
alias ga='git add'
alias gb='git branch'
alias gp='git push'
alias gl='git l'
alias glg='git log'
alias gch='git checkout'
alias gcm='git commit'

# global
alias -g .g='| grep -i'
alias -g .l='| less'
alias -g .c="| column -t"
alias -g .p="| $PAGER"
alias -g .stn='| tr " " "\n"'

# color
alias diff='diff --color=always'
alias watch='watch --color'
alias pacman='pacman --color=always'
alias dmesg='dmesg --color=always'
alias grep='grep -i --color=always'
alias fgrep='fgrep --color=always'
alias egrep='egrep --color=always'
alias gcc='gcc -fdiagnostics-color=always'

# ls
alias ls='ls --color -t'
alias l='colorls -t'
alias ll='colorls -ltr'
alias la='colorls -At'
alias lla='colorls -lAtr'

# confirm
alias rm='rm -i'
alias cp='cp -i'
alias mv='mv -i'
